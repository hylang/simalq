(import
  metadict [MetaDict]
  simalq.color
  simalq.game-state [G]
  simalq.tile [Tile Player]
  simalq.display [color-tile]
  simalq.quest [Quest start-quest])
(setv  T True  F False)


(defn get-info []
  "Compile all the tile info screens into a dictionary."

  (start-quest None)  ; Initialize `G`.
  (setv tiles (lfor
    tt (.values Tile.types)
    (if (is tt Player) G.player (tt))))

  (MetaDict (dfor
    [name superclass] (.items Tile.superclasses)
    name (lfor
      tile tiles
      :if (isinstance tile superclass)
      (dict
        :id (.replace tile.stem " " "-")
        :long-name tile.name-with-article
        :mapsym (lfor
          cc (color-tile tile)
          (dict
            :char cc.char
            :colors (lfor
              color [
                (or cc.fg simalq.color.default-fg)
                (or cc.bg simalq.color.default-bg)]
              (get simalq.color.by-name color))))
        :bullets (lfor
          bullet (.info-bullets tile)
          :if bullet
          bullet)
        :flavor tile.flavor)))))


(defn html []
  "Use `get-info` to construct an HTML document."

  (import
    ; The depenency on `lxml` isn't declared in `setup.py` because it
    ; isn't needed to play the game.
    lxml.html
    lxml.builder)

  (setv E (lxml.builder.ElementMaker
    :makeelement lxml.html.html-parser.makeelement))

  (defn mapsym [tile]
    (E.code :class "mapsym" #* (gfor
      cc tile.mapsym
      (E.span cc.char :style
        (.format "color: {}; background-color: {}" #* (gfor
          rgb cc.colors
          (.format "#{:02x}{:02x}{:02x}" #* rgb)))))))

  (setv info (get-info))

  (setv doc (E.html :lang "en"

    ; The `<head>`
    (E.head
      (E.meta :charset "UTF-8")
      (E.title "Tilepedia — Infinitesimal Quest 2 + ε")
      (E.style "
        h2, h3
           {border-top: thin solid}
        .mapsym
           {white-space: pre;
            margin-right: 1em}
        .flavor
           {white-space: pre-wrap}"))

    ; The page header
    (E.h1 "Tilepedia")
    (E.p
      "This page is a compendium of info screens for every tile type in "
      (E.a :href "https://hylang.org/simalq" "Infinitesimal Quest 2 + ε")
      ". It's generated by "
      (E.code "simalq.tile.tilepedia.html")
      ". It reflects default values for each type; "
      "in game, for example, monsters can have more than 1 HP.")

    ; The table of contents
    (E.nav
      (E.h2 "Contents")
      (E.ul #* (gfor
        [superclass-name tiles] (.items info)
        (E.li superclass-name (E.ul #* (gfor
          tile tiles
          (E.li (mapsym tile) (E.a tile.long-name
            :href (+ "#" tile.id)))))))))

    ; One section of info screens per superclass
    #* (cat (gfor
      [superclass-name tiles] (.items info)
      [(E.h2 superclass-name) #* (cat (gfor
        ; One info screen per tile type
        tile tiles
        [(E.h3 (mapsym tile) tile.long-name :id tile.id)
          (E.ul #* (gfor
            bullet tile.bullets
            (E.li #* (if (isinstance bullet tuple)
              [(E.strong (get bullet 0) ": ") (str (get bullet 1))]
              [(E.strong bullet)]))))
          (E.div :class "flavor" tile.flavor)]))]))))

  (lxml.html.tostring doc
    :pretty-print T :encoding "unicode" :doctype "<!DOCTYPE html>"))


(defn cat [l]
  (sum :start [] l))


(when (= __name__ "__main__")
  (print (html)))
